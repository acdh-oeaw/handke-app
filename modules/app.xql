xquery version "3.0";
module namespace app="http://www.digital-archiv.at/ns/handke-app/templates";
declare namespace tei="http://www.tei-c.org/ns/1.0";
import module namespace functx = "http://www.functx.com";
declare namespace util = "http://exist-db.org/xquery/util";
import module namespace templates="http://exist-db.org/xquery/templates" ;
import module namespace config="http://www.digital-archiv.at/ns/handke-app/config" at "config.xqm";
import module namespace kwic = "http://exist-db.org/xquery/kwic" at "resource:org/exist/xquery/lib/kwic.xql";


(:~
 : This is a sample templating function. It will be called by the templating module if
 : it encounters an HTML element with an attribute data-template="app:test" 
 : or class="app:test" (deprecated). The function has to take at least 2 default
 : parameters. Additional parameters will be mapped to matching request or session parameters.
 : 
 : @param $node the HTML node with the attribute which triggered this call
 : @param $model a map containing arbitrary data - used to pass information between template calls
 :)
declare function app:test($node as node(), $model as map(*)) {
    <p>Dummy template output generated by function app:test at {current-dateTime()}. The templating
        function was triggered by the data-template attribute <code>data-template="app:test"</code>.</p>
};

(:~
: returns the name of the document of the node passed to this function.
:)
declare function app:getDocName($node as node()){
let $name := functx:substring-after-last(document-uri(root($node)), '/')
    return $name
};

(:~
 : href to document.
 :)
declare function app:hrefToDoc($node as node()){
let $name := functx:substring-after-last($node, '/')
let $href := concat('show.html','?document=', app:getDocName($node))
    return $href
};

(:~
 : a fulltext-search function
 :)
 declare function app:ft_search($node as node(), $model as map (*)) {
 if (request:get-parameter("searchexpr", "") !="") then
 let $searchterm as xs:string:= request:get-parameter("searchexpr", "")
 for $hit in collection(concat($config:app-root, '/data/'))//*[.//tei:p[ft:query(.,$searchterm)]|.//tei:cell[ft:query(.,$searchterm)] | .//tei:msDesc[ft:query(.,$searchterm)]]
    let $doc := document-uri(root($hit))
    let $type := tokenize($doc,'/')[(last() - 1)]
    let $params := concat("&amp;directory=", $type, "&amp;stylesheet=", $type)
    let $href := concat(app:hrefToDoc($hit), "&amp;searchexpr=", $searchterm, $params) 
    let $score as xs:float := ft:score($hit)
    order by $score descending
    return
    <tr>
        <td>{$score}</td>
        <td class="KWIC">{kwic:summarize($hit, <config width="40" link="{$href}" />)}</td>
        <td>{app:getDocName($hit)}</td>
    </tr>
 else
    <div>Nothing to search for</div>
 };

(:~
 : fetches all documents which contain the searched entity
 :)
declare function app:registerBasedSearch_hits($node as node(), $model as map(*), $searchkey as xs:string?, $path as xs:string?)
{
for $title in collection(concat($config:app-root, '/data/editions'))//tei:TEI[.//tei:author[.//*[@*=$searchkey]]]
    let $doc := document-uri(root($title))
    let $type := tokenize($doc,'/')[(last() - 1)]
    let $params := concat("&amp;directory=", $type, "&amp;stylesheet=", $type)
    return
    <tr>
        <td>
            <a href="{concat(app:hrefToDoc($title),$params)}">{$title//tei:titleStmt/tei:title/text()}</a>
        </td>
    </tr> 
 };
  
 (:~
 : creates a basic organisation-index derived from the  '/data/indices/listorg.xml'
 :)
declare function app:listOrg($node as node(), $model as map(*)) {
    let $listperson := doc(concat($config:app-root, '/data/indices/listperson.xml'))//tei:TEI
    for $org in doc(concat($config:app-root, '/data/indices/listorg.xml'))//tei:listOrg/tei:org
    let $id := $org/tei:orgName
    let $doc := document-uri(root($org))
    let $type := tokenize($doc,'/')[(last() - 1)]
    let $href := 'entitydetail.html?entityID='||$org/@xml:id
    let $country := $org//tei:country
    order by $country
        return
        <tr>
            <td>
                <a href="{$href}">{$org/tei:orgName}</a>
            </td>
            <td>
                {$country}
            </td>
        </tr>
};
 
(:~
 : creates a basic event-index derived from all event-elemnts in /data/editions/'
:)
declare function app:listEvent($node as node(), $model as map(*)) {
    for $event in collection($config:app-root||'/data/editions')//tei:event
    let $title := root($event)//tei:titleStmt/tei:title[@level='a']/text()[1]
    let $processedTitle := replace(xs:string(functx:substring-after-last($title, '(')), '\)', '')
    let $placeID := substring-after($event/@where, '#')
    let $place := doc($config:app-root||'/data/indices/listplace.xml')//tei:place[@xml:id=$placeID]
    let $coordinates := for $x in tokenize($place//tei:geo/text(), ',') return <coordinates>{number(functx:trim($x))}</coordinates>
    let $type := data($event/@type)
    let $persons := string-join($event//tei:persName//text())
    let $text := normalize-space(string-join($event//text(), ' '))
    let $start := if (exists(data($event/@notBefore))) 
        then data($event/@notBefore) 
        else if (exists(data($event/@from)))
        then data($event/@from)
        else "1980-01-01"
    let $end := if (exists(data($event/@notAfter))) 
        then data($event/@notAfter) 
        else if (exists(data($event/@to)))
        then data($event/@to)
        else "2017-01-01"
        order by $processedTitle
        return
        <tr>
            <td>
                <a href="{app:hrefToDoc($event)}">{$processedTitle}</a>
            </td>
            <td>
                {$type}
            </td>
            <td>
                {$text}
            </td>
            <td>
                {$start} - {$end}
            </td>
            <td>
                {$place/tei:placeName}
            </td>
            <td>
                {$persons}
            </td>
        </tr>
};
 
(:~
 : creates a basic place-index derived from the  '/data/indices/listplace.xml'
 :)
declare function app:listPlace($node as node(), $model as map(*)) {
    let $listperson := doc(concat($config:app-root, '/data/indices/listperson.xml'))//tei:TEI
    for $place in doc(concat($config:app-root, '/data/indices/listplace.xml'))//tei:listPlace/tei:place
    let $id := $place/tei:placeName
    let $doc := document-uri(root($place))
    let $type := tokenize($doc,'/')[(last() - 1)]
    let $href := 'entitydetail.html?entityID='||$place/@xml:id
        return
        <tr>
            <td>
                <a href="{$href}">{$id}</a>
            </td>
            <td>{$place//tei:idno}</td>
            <td><geo>{replace($place//tei:geo/text(),',','|')}</geo></td>
        </tr>
};
 
(:~
 : creates a basic book-index derived from the  '/data/indices/listbook.xml'
 :)
declare function app:listBook($node as node(), $model as map(*)) {
    let $hitHtml := "hits.html?searchkey=boo:"
    for $item in doc(concat($config:app-root, '/data/indices/listbook.xml'))//tei:bibl
    order by $item//tei:surname
        return
        <tr>
            <td>
                <a href="{concat($hitHtml,data($item/@xml:id))}">{$item/tei:author}</a>
            </td>
            <td>{$item//tei:title[@type='full']/text()}</td>
            <td>{$item//tei:pubPlace}</td>
            <td>{$item//tei:date}</td>
        </tr>
};

(:~
 : creates a basic person-index derived from the  '/data/indices/listperson.xml'
 :)
declare function app:listPers($node as node(), $model as map(*)) {
    let $hitHtml := "hits.html?searchkey="
    for $person in doc(concat($config:app-root, '/data/indices/listperson.xml'))//tei:listPerson/tei:person
    let $ref := data($person/tei:persName/@ref)
    let $normID := if (starts-with($ref, 'http')) then <a href="{$ref}">{$ref}</a> else 'no normdata-ID found'
    let $placeID := substring-after($person//tei:residence/@ref, '#')
    let $place := doc($config:app-root||'/data/indices/listplace.xml')//tei:place[@xml:id=$placeID]
    let $affiliationID := substring-after($person//tei:affiliation/@ref, '#')
    let $affiliation := doc($config:app-root||'/data/indices/listorg.xml')//tei:org[@xml:id=$affiliationID]
    let $href := 'entitydetail.html?entityID='||$person/@xml:id
    order by $person//tei:surname
        return
        <tr>
            <td>
                <a href="{$href}">{$person//tei:surname}</a>
            </td>
            <td>{$person//tei:forename}</td>
            <td>{$place//tei:placeName} ({$place//tei:geo})</td>
            <td>{$affiliation//tei:orgName/text()}</td>
            <td>{$normID}</td>
        </tr>
};

(:~
 : creates a basic table of content derived from the documents stored in '/data/editions'
 :)
declare function app:toc($node as node(), $model as map(*)) {
    for $doc in collection(concat($config:app-root, '/data/editions'))//tei:TEI
    let $collection := functx:substring-after-last(util:collection-name($doc), '/')
    let $title := $doc//tei:titleStmt/tei:title[@level='a']/text()[1]
    let $processedTitle := xs:string(functx:substring-after-last($title, '('))
    let $subtitle := $doc//tei:msItem/tei:bibl/tei:title
    let $date := $doc//tei:event[@type='totalduration']
(:    let $sortKey := $authors[1]/tei:surname/text()
    order by $sortKey:)
        return
        <tr>
            <td>
                <a href="{concat(app:hrefToDoc($doc),'&amp;directory=',$collection,'&amp;stylesheet=',$collection)}">
                    {replace($processedTitle, '\)', '')}
                </a>
            </td>
            <td>
                {string-join($subtitle//text(), ' ')}
            </td>
            <td>
                {data($date/@from)} - {data($date/@to)}
            </td>
            <td>
                {($doc//tei:msDesc/tei:msIdentifier/tei:repository/text())[1]}
            </td>
            <td>
                {($doc//tei:msDesc/tei:msIdentifier/tei:idno/text())[1]}
            </td>
        </tr>   
};

(:~
 : perfoms an XSLT transformation
:)
declare function app:XMLtoHTML ($node as node(), $model as map (*), $query as xs:string?) {
let $ref := xs:string(request:get-parameter("document", ""))
let $xmlPath := concat(xs:string(request:get-parameter("directory", "editions")), '/')
let $xml := doc(replace(concat($config:app-root,'/data/', $xmlPath, $ref), '/exist/', '/db/'))
let $xslPath := concat(xs:string(request:get-parameter("stylesheet", "editions")), '.xsl')
let $xsl := doc(replace(concat($config:app-root,'/resources/xslt/', $xslPath), '/exist/', '/db/'))
let $params := 
<parameters>
   {for $p in request:get-parameter-names()
    let $val := request:get-parameter($p,())
    where  not($p = ("document","directory","stylesheet"))
    return
       <param name="{$p}"  value="{$val}"/>
   }
</parameters>
return 
    transform:transform($xml, $xsl, $params)
};

(:~
: fetches all entities referenced by the current one
:)
declare function app:referencedBy($node as node(), $model as map(*)) {
    let $id := xs:string(request:get-parameter("entityID", ""))
    let $ref := '#'||$id
    for $x in collection(concat($config:app-root,'/data/indices/'))//*[@ref=$ref]/ancestor::*[@xml:id]
        let $xmlID := data($x/@xml:id)
        let $href := 'entitydetail.html?entityID='||$xmlID
        return
            <li>
                <a href="{$href}">{string-join($x//text(), ' ')}</a>
            </li>
};
(:~
: fetches all entities referenced by the current one
:)
declare function app:references($node as node(), $model as map(*)) {
    let $id := xs:string(request:get-parameter("entityID", ""))
    let $ref := '#'||$id
    for $x in collection(concat($config:app-root,'/data/indices/'))//*[@xml:id=$id]//*[@ref]
        let $refID := substring-after(data($x/@ref), '#')
        let $entity := collection(concat($config:app-root,'/data/indices/'))//*[@xml:id=$refID]
        let $href := 'entitydetail.html?entityID='||$refID
        where $refID != ''
        return
            <li>
                <a href="{$href[1]}">{$entity}</a>
            </li>
};

(:~
: fetches entity from index files and returns it
:)
declare function app:showEntityInfo($node as node(), $model as map(*)) {
    let $ref := xs:string(request:get-parameter("entityID", ""))
    let $element := collection(concat($config:app-root,'/data/indices/'))//*[@xml:id=$ref]
    for $x in $element 
    return
        string-join($x//text())
};